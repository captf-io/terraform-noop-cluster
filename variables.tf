# Copyright 2026 The CAPTF Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Contract inputs of the cluster role, v1alpha1 (https://docs.captf.io/module-author/contract/v1alpha1/common.html
# and cluster.html). The controller sets every one; the no-op module
# records them in terraform_data so a plan shows what it was given.

variable "captf_contract" {
  description = "Contract version the controller generated the root for; always v1alpha1."
  type        = string
}

variable "captf_cluster" {
  description = "The owning CAPI Cluster: name and namespace."
  type = object({
    name      = string
    namespace = string
  })
}

variable "captf_object" {
  description = "The TerraformCluster being reconciled: kind, name and namespace."
  type = object({
    kind      = string
    name      = string
    namespace = string
  })
}

# No captf_cluster_outputs: the cluster role never receives it (see the
# contract CHANGELOG). The skeleton's defaulted declaration is optional.

variable "captf_tags" {
  description = "Tags the controller always sets (captf.io/cluster, captf.io/namespace, captf.io/kind, captf.io/name, captf.io/managed-by, captf.io/template); held in terraform_data like every other input."
  type        = map(string)
}

variable "control_plane_endpoint" {
  description = "An endpoint the module does not own (set by the user or a control-plane provider). Non-null is passed through as the control_plane_endpoint output."
  type = object({
    host = string
    port = number
  })
  default = null
}

variable "kubernetes_version" {
  description = "Cluster.spec.topology.version, null without ClusterClass. Held, otherwise unused."
  type        = string
  default     = null
}

variable "control_plane_initialized" {
  description = "Cluster.status.initialization.controlPlaneInitialized, latched. Held, otherwise unused."
  type        = bool
}

variable "cluster_network" {
  description = "Cluster.spec.clusterNetwork. Held, otherwise unused."
  type = object({
    pods            = optional(list(string), [])
    services        = optional(list(string), [])
    service_domain  = optional(string)
    api_server_port = optional(number)
  })
  default = null
}
